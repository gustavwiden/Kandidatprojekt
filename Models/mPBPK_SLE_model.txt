########## NAME
// Comments are made with '//'
mPBPK_SLE_model
########## METADATA
time_unit = h
########## MACROS
########## STATES
d/dt(Plasma_total) = r1+r2-r3-r4-r5-r9-r10-r11
d/dt(Skin_total) = r3-r6-r12
d/dt(Muscle) = r4-r7
d/dt(Leaky) = r5-r8
d/dt(Lymph) = r6+r7+r8-r2
d/dt(Depot) = -r1
d/dt(BDCA2_plasma_total) = r13-r15-r17
d/dt(BDCA2_skin_total) = r14-r16-r18

Plasma_total(0) = 0
Skin_total(0) = 0
Muscle(0) = 0
Leaky(0) = 0
Lymph(0) = 0
Depot(0) = 0
BDCA2_plasma_total(0) = 1.26e-2 // 1 010 000 MESF baseline converted to μg with molecular weight for BDCA2 of 25.021 kDa. 300 pDCs/ml [SCALE with 1000 to plot PD without oscialltions]
BDCA2_skin_total(0) = 1.26 // [SCALE with 1000 to plot PD without oscialltions]
########## PARAMETERS
F = 0.679           // Bioavailibility
ka = 0.01           // Absorption rate (1/h)
Vp = 2600           // Volume of plasma compartment (mL)
Vs = 1810           // Volume of skin compartment (mL)
V1 = 6300           // Volume of muscle compartment (mL)
V2 = 4370           // Volume of leaky tissue compartment (mL)
VL = 2600           // Volume of lymph compartment (mL)
Ls = 10.29          // Skin lymph flow rate (mL/h)
L1 = 29.58          // Muscle lymph flow rate (mL/h)
L2 = 80.96          // Leaky tissue lymph flow rate (mL/h)
RCS = 0.769         // Reflection co-efficient skin
RC1 = 0.95          // Reflection co-efficient muscle
RC2 = 0.605         // Reflection co-efficient leaky tissue
RCL = 0.20          // Reflection co-efficient lymph
SLE_CL = 10.43       // Linear clearance for SLE patient (mL/h)
Vm = 20900          // Maximum rate achieved by the system (μg/h) [SCALE with 1000 to plot PD without oscialltions]
Km = 281            // Substrate concentration (μg/mL) where reaction velocity is 50% of Vm [SCALE with 1000 to plot PD without oscialltions]
ksyn = 1.31e-1      // 1 052 000 MESF/h converted to μg/h. [SCALE with 1000 to plot PD without oscialltions]
kint = 8            // Optimized from guess, however value kept below 10 for physiologically reasonable params
kd = 525            // 0.35 μg/ml found for 24F4 (similair antibody) [SCALE with 1000 to plot PD without oscialltions]
kmig = 0.07         // Migration of pDCs from plasma to skin (1/h)

########## VARIABLES
Plasma_free = 0.5*((Plasma_total-kd-BDCA2_plasma_total)+sqrt((Plasma_total-kd-BDCA2_plasma_total)^2+4*Plasma_total*kd))
Skin_free = 0.5*((Skin_total-kd-BDCA2_skin_total)+sqrt((Skin_total-kd-BDCA2_skin_total)^2+4*Skin_total*kd))
BDCA2_plasma_bound = BDCA2_plasma_total * Plasma_free / (kd + Plasma_free)
BDCA2_skin_bound = BDCA2_skin_total * Skin_free / (kd + Skin_free)
BDCA2_plasma_free = BDCA2_plasma_total - BDCA2_plasma_bound
BDCA2_skin_free = BDCA2_skin_total - BDCA2_skin_bound
kdegp = ksyn/(1.26e-2/Vp) // [SCALE with 1000 to plot PD without oscialltions]
kdegs = (kmig*BDCA2_plasma_total/Vp)/(1.26) // [SCALE with 1000 to plot PD without oscialltions]

r1 = ka*Depot
r2 = (Ls+L1+L2)*Lymph/VL
r3 = Ls*(1-RCS)*Plasma_free/Vp
r4 = L1*(1-RC1)*Plasma_free/Vp
r5 = L2*(1-RC2)*Plasma_free/Vp
r6 = Ls*(1-RCL)*Skin_free/Vs
r7 = L1*(1-RCL)*Muscle/V1
r8 = L2*(1-RCL)*Leaky/V2
r9 = SLE_CL*Plasma_free/Vp
r10 = Plasma_free/Vp*Vm/(Km+Plasma_free/Vp)
r11 = kint*BDCA2_plasma_bound
r12 = kint*BDCA2_skin_bound
r13 = ksyn
r14 = kmig*BDCA2_plasma_bound/Vp
r15 = kdegp*(BDCA2_plasma_total/Vp-BDCA2_plasma_bound/Vp)
r16 = kdegs*(BDCA2_skin_total/Vs-BDCA2_skin_bound/Vs)
r17 = kint*BDCA2_plasma_bound/Vp
r18 = kint*BDCA2_skin_bound/Vs

########## FUNCTIONS
########## EVENTS
IV_dose = IV > 0, Plasma_total, Plasma_total + IV*1000 // [SCALE with 1000 to plot PD without oscialltions]
SC_dose = SC > 0, Depot, (Depot + SC)*F*1000 // [SCALE with 1000 to plot PD without oscialltions]
########## OUTPUTS
########## INPUTS
IV = IV_in @ 0
SC = SC_in @ 0
########## FEATURES
//Output not used needs to be commented out in scripts that calculates cost

PK_sim = Plasma_total/(Vp*1000)
PD_sim = 100*((BDCA2_plasma_total-BDCA2_plasma_bound-1.26e-2)/1.26e-2)



