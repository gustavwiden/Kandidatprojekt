########## NAME
// Comments are made with '//'
mPBPK_model
########## METADATA
time_unit = h
########## MACROS
########## STATES
d/dt(Plasma) = (r2+r3-r4-r5-r6-r10)/Vp-r13
d/dt(Skin) = (r4-r7)/Vs
d/dt(Tight) = (r5-r8)/V1
d/dt(Leaky) = (r6-r9)/V2
d/dt(Lymph) = (-r3+r7+r8+r9)/VL
d/dt(Depot) = -r1
d/dt(BDCA2_plasma) = r11-r12-r13

Plasma(0) = 0                      // Total BIIB059 concentration (pM) in plasma compartment.
Skin(0) = 0                         // Total BIIB059 concentration (pM) in skin compartment.
Tight(0) = 0                       // Total BIIB059 concentration (pM) in tight compartment.
Leaky(0) = 0                        // Total BIIB059 concentration (pM) in leaky compartment.
Lymph(0) = 0                        // Total BIIB059 concentration (pM) in lymph compartment.
Depot(0) = 0                        // Total BIIB059 concentration (pM) in depot compartment.
BDCA2_plasma(0) = 23.1              // Total BDCA2 concentration (pM) in blood. 1.01e6 BDCA2/pDC. 5.1e6 pDCs/L blood.
########## PARAMETERS
F = 0.649           // Bioavailibility.
ka = 0.0146         // Absorption rate (1/h).
Vp = 2.6            // Plasma volume (L).
Vs = 1.125          // ISF volume in skin (L).
V1 = 6.987          // ISF volume in tight tissues (L).
V2 = 4.368          // ISF volume in leaky tissues (L).
VL = 2.6            // Lymph volume (L).
Ls = 0.0103         // Skin lymph flow rate (L/h).
L1 = 0.0296         // Muscle lymph flow rate (L/h).
L2 = 0.081          // Leaky tissue lymph flow rate (L/h).
RCS = 0.77          // Reflection co-efficient for skin compartment.
RC1 = 0.95          // Reflection co-efficient for tight compartment.
RC2 = 0.605         // Reflection co-efficient for leaky compartment.
RCL = 0.20          // Reflection co-efficient for lymph compartment.
CL = 0.0057         // Linear clearance of BIIB059 from plasma compartment.
kdeg = 4.05       // Degradation rate of BDCA2 (pM/h). Converted from 263 000 MESF/h
kint = 5.54        // Internalization rate (1/h)
kss = 2624

########## VARIABLES
ksynp = kdeg*23.1 
Plasma_free = 0.5*((Plasma - kss - BDCA2_plasma) + sqrt((Plasma - kss - BDCA2_plasma)^2 + 4*Plasma*kss))      // Concentration (pM) of free BIIB059 in plasma
BDCA2_plasma_bound = (BDCA2_plasma * Plasma_free) / (kss + Plasma_free)

r1 = ka*Depot
r2 = F*ka*Depot
r3 = (Ls+L1+L2)*Lymph
r4 = Ls*(1-RCS)*Plasma_free
r5 = L1*(1-RC1)*Plasma_free
r6 = L2*(1-RC2)*Plasma_free
r7 = Ls*(1-RCL)*Skin
r8 = L1*(1-RCL)*Tight
r9 = L2*(1-RCL)*Leaky
r10 = CL*Plasma_free
r11 = ksynp
r12 = kdeg*(BDCA2_plasma-BDCA2_plasma_bound)
r13 = kint*BDCA2_plasma_bound


########## FUNCTIONS
########## EVENTS
IV_dose = IV > 0, Plasma, Plasma + IV*1e6/(146600*Vp)  
SC_dose = SC > 0, Depot, Depot + SC*1e6/(146600)  
########## OUTPUTS
########## INPUTS
IV = IV_in @ 0
SC = SC_in @ 0
########## FEATURES

PK_sim = (Plasma_free*146600)/(1e9)                     // Output is serum concentration of BIIB059 in Î¼g/ml. BIIB059 have a molecular weigt of 146.6 kDa.
PD_sim = 100*((BDCA2_plasma-23.1)/23.1)           // Output is the percentage change from baseline of BDCA2 expression on pDCs
PK_sim_skin = Skin*146600/1e9
