########## NAME
// Comments are made with '//'
mPBPK_model
########## METADATA
time_unit = h
########## MACROS
########## STATES
d/dt(Plasma_total) = r1+r2-r3-r4-r5-r9-r10-r11
d/dt(Skin_total) = r3-r6-r12
d/dt(Muscle) = r4-r7
d/dt(Leaky) = r5-r8
d/dt(Lymph) = r6+r7+r8-r2
d/dt(Depot) = -r1
d/dt(BDCA2_plasma_total) = r13-r15-r17
d/dt(BDCA2_skin_total) = r14-r16-r18

Plasma_total(0) = 0
Skin_total(0) = 0
Muscle(0) = 0
Leaky(0) = 0
Lymph(0) = 0
Depot(0) = 0
BDCA2_plasma_total(0) = 1.05e-4 // 1 010 000 MESF baseline converted to μg with molecular weight for BDCA2 of 25.021 kDa. 5000 pDCs/ml blood.
BDCA2_skin_total(0) = 1e-7 // Assumed to be almost non existent in HV, however should be higher for SLE patients
########## PARAMETERS
F = 0.713 // Bioavailibility
ka = 0.00975 // Absorption rate
Vp = 2600 // volume of plasma compartment (mL)
Vs = 1810 // volume of skin compartment (mL)
V1 = 6300 // volume of muscle compartment (mL)
V2 = 4370 // volume of leaky tissue compartment (mL)
VL = 2600 // volume of lymph compartment (mL)
Ls = 10.29 // Skin lymph flow rate (mL/h)
L1 = 29.58 // Muscle lymph flow rate (mL/h)
L2 = 80.96 // Leaky tissue lymph flow rate (mL/h)
RCS = 0.63 // Reflection co-efficient skin
RC1 = 0.95 // Reflection co-efficient muscle
RC2 = 0.36 // Reflection co-efficient leaky tissue
RCL = 0.20 // Reflection co-efficient lymph
HV_CL = 5.52 // Linear clearance for healthy volunteer (mL/h). SLE_CL = 6.87. Linear clearance for SLE subject
Vm = 10.7 // Maximum rate achieved by the system (μg/h)
Km = 0.547 // Substrate concentration (μg/mL) where reaction velocity is 50% of Vm
ksynp = 1.92e-4 // 526 000 MESF/h converted to μg/h
ksyns = 5e-8 // Using the same ratio between between baseline and ksyn as in plasma
kintp = 4 // Guess
kints = 4 // Guess
kd = 0.35 // 10 nM found for 24F4 (similair antibody), converted to μg/ml

########## VARIABLES
Plasma_free = 0.5*((Plasma_total-kd-BDCA2_plasma_total)+sqrt((Plasma_total-kd-BDCA2_plasma_total)^2+4*Plasma_total*kd))
Skin_free = 0.5*((Skin_total-kd-BDCA2_skin_total)+sqrt((Skin_total-kd-BDCA2_skin_total)^2+4*Skin_total*kd))
BDCA2_plasma_bound = BDCA2_plasma_total * Plasma_free / (kd + Plasma_free)
BDCA2_skin_bound = BDCA2_skin_total * Skin_free / (kd + Skin_free)
BDCA2_plasma_free = BDCA2_plasma_total - BDCA2_plasma_bound
BDCA2_skin_free = BDCA2_skin_total - BDCA2_skin_bound
kdegp = ksynp/(1.05e-4/Vp)
kdegs = ksyns/(1e-7/Vs)

r1 = ka*Depot
r2 = (Ls+L1+L2)*Lymph/VL
r3 = Ls*(1-RCS)*Plasma_free/Vp
r4 = L1*(1-RC1)*Plasma_free/Vp
r5 = L2*(1-RC2)*Plasma_free/Vp
r6 = Ls*(1-RCL)*Skin_free/Vs
r7 = L1*(1-RCL)*Muscle/V1
r8 = L2*(1-RCL)*Leaky/V2
r9 = HV_CL*Plasma_free/Vp
r10 = Plasma_free/Vp*Vm/(Km+Plasma_free/Vp)
r11 = kintp*BDCA2_plasma_bound
r12 = kints*BDCA2_skin_bound
r13 = ksynp
r14 = ksyns
r15 = kdegp*(BDCA2_plasma_total/Vp-BDCA2_plasma_bound/Vp)
r16 = kdegs*(BDCA2_skin_total/Vs-BDCA2_skin_bound/Vs)
r17 = kintp*BDCA2_plasma_bound/Vp
r18 = kints*BDCA2_skin_bound/Vs

########## FUNCTIONS
########## EVENTS
IV_dose = IV > 0, Plasma_total, Plasma_total + IV
SC_dose = SC > 0, Depot, (Depot + SC)*F
########## OUTPUTS
########## INPUTS
IV = IV_in @ 0
SC = SC_in @ 0
########## FEATURES
//y_sim = Plasma_total/Vp
y_sim = 100 * ((BDCA2_plasma_total-BDCA2_plasma_bound-1.05e-4)/1.05e-4)



